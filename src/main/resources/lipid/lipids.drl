package lipid;
unit LipidScoreUnit;

import lipid.Annotation
import static lipid.Lipid.lipidTypeRank


rule "Score 1 for lipid pair with increasing RT and carbon count"
when
    $a1 : /annotations [$rt1 : rtMin, $carbonCount1 : lipid.getCarbonCount(),
    $doubleBondCount : lipid.getDoubleBondsCount(), $lipidType1 : lipid.getLipidType()]

    $a2 : /annotations [this!= $a1, lipid.getDoubleBondsCount() == $doubleBondCount, lipid.getLipidType() == $lipidType1,
    rtMin > $rt1, lipid.getCarbonCount() > $carbonCount1]
then
    $a1.addScore(1);
    $a2.addScore(1);
    // System.out.println("Rule 1: Scored 1 for annotations: " + $a1 + " and " + $a2);
end


rule "Score 1 for lipid pair with increasing RT and less double bond count"
when
    $a1 : /annotations [$rt1 : rtMin, $carbonCount1 : lipid.getCarbonCount(),
    $doubleBondCount : lipid.getDoubleBondsCount(), $lipidType1 : lipid.getLipidType()]

    $a2 : /annotations [this!= $a1,lipid.getCarbonCount() == $carbonCount1 , lipid.getLipidType()== $lipidType1,
    rtMin > $rt1, lipid.getDoubleBondsCount() < $doubleBondCount ]

then
    $a1.addScore(1);
    $a2.addScore(1);
    //System.out.println("Rule  2: Scored 1 for annotations: " + $a1 + " and " + $a2);
end


rule "Score 1 for lipid pair with increasing RT and different lipidType"
when
    $a1 : /annotations [$rt1 : rtMin, $carbonCount1 : lipid.getCarbonCount(),
    $doubleBondCount : lipid.getDoubleBondsCount(), $lipidType1 : lipid.getLipidType()]

    $a2 : /annotations [this!= $a1, lipid.getDoubleBondsCount() == $doubleBondCount, lipid.getCarbonCount() == $carbonCount1,
     $lipidType2 : lipid.getLipidType(), Lipid.lipidTypeRank($lipidType2) > Lipid.lipidTypeRank($lipidType1), rtMin > $rt1]

then
    $a1.addScore(1);
    $a2.addScore(1);
    // System.out.println("Rule 3: Scored 1 for annotations: " + $a1 + " and " + $a2);
end




rule "Score -1 for lipid pair with decreasing RT when carbon count decreases"
when
    $a1 : /annotations [$rt1 : rtMin, $carbonCount1 : lipid.getCarbonCount(),
    $doubleBondCount : lipid.getDoubleBondsCount(), $lipidType1 : lipid.getLipidType()]

    $a2 : /annotations [this!= $a1, lipid.getDoubleBondsCount() == $doubleBondCount,
   lipid.getLipidType() == $lipidType1, rtMin < $rt1, lipid.getCarbonCount() > $carbonCount1]

then
    $a1.addScore(-1);
    $a2.addScore(-1);
//    System.out.println("Rule 4: Scored -1 for annotations: " + $a1 + " and " + $a2);
end


rule "Score -1 for lipid pair with decreasing RT when double bond decreases"
when
    $a1 : /annotations [$rt1 : rtMin, $carbonCount1 : lipid.getCarbonCount(),
    $doubleBondCount : lipid.getDoubleBondsCount(), $lipidType1 : lipid.getLipidType()]

    $a2 : /annotations [this!= $a1, lipid.getCarbonCount() == $carbonCount1,
   lipid.getLipidType() == $lipidType1, (lipid.getDoubleBondsCount() < $doubleBondCount && rtMin < $rt1) ]

then
    $a1.addScore(-1);
    $a2.addScore(-1);
//    System.out.println("Rule 5: Scored -1 for annotations: " + $a1 + " and " + $a2);
end


rule "Score -1 for lipid pair with decreasing RT and different lipidType"
when
    $a1 : /annotations [$rt1 : rtMin, $carbonCount1 : lipid.getCarbonCount(),
    $doubleBondCount : lipid.getDoubleBondsCount(), $lipidType1 : lipid.getLipidType()]

    $a2 : /annotations [this!= $a1, lipid.getDoubleBondsCount() == $doubleBondCount, lipid.getCarbonCount() == $carbonCount1,
     $lipidType2 : lipid.getLipidType(), Lipid.lipidTypeRank($lipidType2) > Lipid.lipidTypeRank($lipidType1), rtMin < $rt1]

then
    $a1.addScore(-1);
    $a2.addScore(-1);
    // System.out.println("Rule 6: Scored -1 for annotations: " + $a1 + " and " + $a2);
end